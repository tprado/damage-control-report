apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'maven'
apply plugin: 'signing'

codenarc.configFile = file('../codenarc.groovy')

dependencies {
    compile         group: 'org.codehaus.groovy',   name: 'groovy-all',     version: groovyVersion
    compile         group: 'commons-io',            name: 'commons-io',     version: '1.3.2'
    testCompile     group: 'org.spockframework',    name: 'spock-core',     version: spockVersion
}

task cleanLocalMavenRepository(type: Delete) {
    delete "${localMavenRepositoryPath}/html-generator"
}

clean.dependsOn cleanLocalMavenRepository

task generateConfig {
    def config = file("${projectDir}/src/main/resources/damage-control-config.groovy")
    config.text = "version='${version}'\n"
}

compileGroovy.dependsOn generateConfig

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from 'build/docs/groovydoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives groovydocJar
    archives sourcesJar
}

signing {
    required { isReleaseBuild }
    sign configurations.archives
}

uploadArchives {
    doFirst checkMavenRepositoryCredentials

    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: System.env.DCR_SONATYPE_USERNAME, password: System.env.DCR_SONATYPE_PASSWORD)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: System.env.DCR_SONATYPE_USERNAME, password: System.env.DCR_SONATYPE_PASSWORD)
            }

            pom.project {
                name 'Damage Control Report - HTML Generator'
                description 'Damage Control Report - HTML Generator'

                url 'https://github.com/damage-control/report'

                scm {
                    url 'scm:git@github.com:damage-control/report.git'
                    connection 'scm:git@github.com:damage-control/report.git'
                    developerConnection 'scm:git@github.com:damage-control/report.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'gsfreddo'
                        name 'Giordana Sfreddo'
                        email 'gsfreddo@thoughtworks.com'
                    }
                    developer {
                        id 'tprado'
                        name 'Thiago Prado'
                        email 'tprado@thoughtworks.com'
                    }
                }
            }
        }
    }
}

install.dependsOn check, test

uploadArchives.dependsOn ':end-2-end-test:test'
