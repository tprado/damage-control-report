defaultTasks 'clean', 'test'

ext {
    groovyVersion = '2.4.3'
    spockVersion = '1.0-groovy-2.4'
    damageControlReportGroup = 'com.github.damage-control.report'
    damageControlReportVersion = '2.0-SNAPSHOT'
    isReleaseBuild = !damageControlReportVersion.endsWith('SNAPSHOT')
    localMavenRepositoryPath = "${System.env.HOME}/.m2/repository/com/github/damage-control/report"

    checkMavenLocation = {
        if (!System.env.M2_HOME && !System.env.MVN_COMMAND) {
            throw new GradleException('M2_HOME nor MVN_COMMAND environment variables not defined.')
        }
    }

    checkMavenRepositoryCredentials = {
        if (!System.env.DCR_SONATYPE_USERNAME || !System.env.DCR_SONATYPE_PASSWORD) {
            throw new GradleException('DCR_SONATYPE_USERNAME or DCR_SONATYPE_PASSWORD environment variables not defined (required for uploading artifacts).')
        }
    }
}

subprojects { project ->
    group = damageControlReportGroup
    version = damageControlReportVersion

    repositories {
        mavenCentral()
    }

    if (isReleaseBuild) {
        if (!System.env.DCR_SIGNING_PASSWORD) {
            throw new GradleException('DCR_SIGNING_PASSWORD environment variable not defined (required for signing artifacts).')
        }

        ext."signing.keyId" = 'C17D777D'
        ext."signing.secretKeyRingFile" = "${project.rootProject.projectDir}/gnupg/secring.gpg"
        ext."signing.password" = System.env.DCR_SIGNING_PASSWORD
    }
}
